<program> ::= { <statement> | <comment> }
<statement> ::= <simple_statement> | <logical_statement> | <if_statement> | <while_statement> | <for_statement>
<simple_statement> ::= <set_statement> | <input_statement> | <arithmetic_statement> | <clc_statement> | <print_statement>
<set_statement> ::= "SET" <identifier> <value>
<input_statement> ::= "INPUT" <identifier>
<arithmetic_statement> ::= ("ADD" | "SUB" | "MUL" | "DIV" | "MOD") <identifier> <value>
<clc_statement> ::= "CLC" <identifier> <identifier> <operator> <identifier>
<print_statement> ::= "PRINT" { <string> | <identifier> | <value> }
<logical_statement> ::= ("AND" | "OR" | "XOR") <identifier> <value> | "NOT" <identifier>
<if_statement> ::= "IF" <condition> "THEN" <block> { <elif_statement> } [ <else_statement> ] "ENDIF"
<elif_statement> ::= "ELIF" <condition> "THEN" <block>
<else_statement> ::= "ELSE" <block>
<while_statement> ::= "WHILE" <condition> "DO" <block> "ENDWHILE"
<for_statement> ::= "FOR" <identifier> "FROM" <value> "TO" <value> [ "STEP" <value> ] "DO" <block> "ENDFOR"
<block> ::= { <statement> | <comment> }
<condition> ::= <expression> { ("AND" | "OR") <expression> }
<expression> ::= [ "NOT" ] <simple_expression>
<simple_expression> ::= <identifier> <comparison_op> <value> | <identifier>
<comparison_op> ::= "==" | "!=" | "<" | ">" | "<=" | ">="
<comment> ::= "//" { <any_character> }
<identifier> ::= <letter> { <letter> | <digit> | "_" }
<value> ::= <number> | <boolean> | <string> | <identifier>
<number> ::= <integer> | <float>
<integer> ::= <digit> { <digit> }
<float> ::= <integer> "." <integer>
<boolean> ::= "TRUE" | "FALSE"
<string> ::= '"' { <any_character> } '"'
<operator> ::= "ADD" | "SUB" | "MUL" | "DIV" | "MOD"
<letter> ::= [A-Za-z]
<digit> ::= [0-9]
<any_character> ::= [^"] | .
